import Quickshell
import Quickshell.Io
import QtQuick
import Quickshell.Widgets
import QtQuick.Layouts

import qs.Objects.Design
import qs.Objects.Window
import qs.Objects.Widgets

IconButton {
    id: bluetoothWidget
    iconName: "bluetooth"
    iconSize: 25
    tooltipText: "Bluetooth"
    color: root.settings.theme.secondary

    property bool powered: false
    property int connectedCount: 0

    BluetoothPopup {
        id: bluetoothPopup
    }

    Timer {
        interval: 3000
        running: true
        repeat: true
        onTriggered: updateState()
    }

    Process {
        id: stateProc
        command: root.newUtill(["--btstate"])
        stdout: StdioCollector {
            onStreamFinished: {
                var text = this.text.trim()
                if (!text) return
                var obj = {}
                text.split(",").forEach(function(pair) {
                    var kv = pair.split(":")
                    if (kv.length >= 2) obj[kv[0]] = kv.slice(1).join(":")
                })
                bluetoothWidget.powered = obj["powered"] === "yes"
                bluetoothWidget.setIcon(bluetoothWidget.powered ? "bluetooth" : "bluetooth_disabled")
                bluetoothWidget.setColor(bluetoothWidget.powered ? root.settings.theme.primary : root.settings.theme.secondary)
                bluetoothWidget.tooltipText = bluetoothWidget.powered
                    ? "Bluetooth: On"
                    : "Bluetooth: Off"
            }
        }
    }

    function updateState() {
        if (!stateProc.running) stateProc.running = true
    }

    Component.onCompleted: updateState()

    onClicked: bluetoothPopup.toggle(bluetoothWidget)
}
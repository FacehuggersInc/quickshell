import Quickshell
import Quickshell.Io
import QtQuick
import Quickshell.Widgets
import QtQuick.Layouts
import QtQuick.Controls
import QtQuick.Controls.Material


import qs.Objects.Window
import qs.Objects.Design
import qs.Objects.Widgets

RoundedBlock{
    id: appBarWidget

    property var contextIcon
    property var contextTarget
    property var contextPopupObject: null
    property bool addedStaticApps: false
    property ListModel apps: ListModel{}
    property var appStore: ({})

    property var queuedAppClassesForIcons: ([])

    // Args that are never meaningful to save as launch options —
    // typically DBus activation, daemon, or session-management flags
    // that only make sense when the desktop environment spawns the app.
    readonly property var stripArgs: [
        "--gapplication-service",
        "--gapplication-replace",
        "--daemon",
        "-d",
        "--no-desktop",
        "--session",
        "--ozone-platform-hint=auto",
        "--enable-features=WaylandWindowDecorations",
        "--started-from-file",
    ]

    // FUNCTIONS
    function encodeOptions(options) {
        try {
            return JSON.stringify(options || [])
        } catch(e) {
            return "[]"
        }
    }

    function decodeOptions(options) {
        if (!options) return []
        if (typeof options === "string") {
            try {
                return JSON.parse(options)
            } catch(e) {
                return []
            }
        }
        return options
    }

    function cleanArgs(args) {
        // Strip known bad args.
        // Also drop any arg that looks like an instance-specific path or
        // socket (e.g. /run/user/1000/..., /tmp/...) since those will
        // never be valid on a fresh launch.
        var out = []
        for (var i = 0; i < args.length; i++) {
            var a = args[i]
            if (stripArgs.indexOf(a) !== -1) continue
            if (a.startsWith("/run/") || a.startsWith("/tmp/") || a.startsWith("/proc/"))
                continue
            out.push(a)
        }
        return out
    }

    function parseCommand(fullCommand, className) {
        if (!fullCommand || fullCommand.trim() === "")
            return { command: "", options: [] }

        var tokens = fullCommand.trim().split(/\s+/)

        function isPath(t) {
            return t.indexOf("/") === 0
        }

        function isInterpreter(t) {
            return (
                t === "python" ||
                t === "python3" ||
                t === "bash" ||
                t === "sh" ||
                t === "node"
            )
        }

        if (className) {
            for (var i = 0; i < tokens.length; i++) {
                var classParts = className.toLowerCase().split(".")
                if (tokens[i].trim().includes(classParts[classParts.length - 1])) {
                    var launch = tokens[i]
                    var args = cleanArgs(tokens.slice(i + 1))
                    return {
                        command: launch,
                        options: args.length > 0 ? [args] : []
                    }
                }
            }
        }

        if (tokens.length >= 2 && isInterpreter(tokens[0]) && isPath(tokens[1])) {
            var launch = tokens[0] + " " + tokens[1]
            var args = cleanArgs(tokens.slice(2))
            return {
                command: launch,
                options: args.length > 0 ? [args] : []
            }
        }

        if (isPath(tokens[0])) {
            var launch = tokens[0]
            var args = cleanArgs(tokens.slice(1))
            return {
                command: launch,
                options: args.length > 0 ? [args] : []
            }
        }

        return {
            command: fullCommand,
            options: []
        }
    }

    function getProcessCount(name){
        return appStore[name].procs.length
    }

    function getHiddenCount(name) {
        var state = appStore[name]
        var list = state ? state.procs : []
        var count = 0

        for (var i = 0; i < list.length; i++) {
            if (list[i].workspace === "special:hidden") {
                count++
            }
        }

        return count
    }

    function getPIDs(name) {
        var state = appStore[name]
        var list = state ? state.procs : []
        return list.map(x => x.pid)
    }

    function getAppStateFromPID(pid){
        for (var name in appStore){
            var value = appStore[name]
            if (!value) continue
            for (var i=0; i < value.procs.length; i++){
                var instance = value.procs[i]
                if (instance.pid === pid){
                    return instance
                }
            }
        }
        return undefined
    }

    function isAppPinned(name){
        var pinned = false
        for (var i=0; i < root.settings.launchers.length; i++){
            var item = root.settings.launchers[i]
            if (item.name === name){
                pinned = true
                break
            }
        }
        return pinned
    }

    function clearInactiveApps(){
        for (var i = apps.count - 1; i >= 0; i--) {
            var app = apps.get(i)
            if (!app.type.includes("static") && getPIDs(app.name).length === 0) {
                apps.remove(i)
            }
        }
    }

    function getActiveApps(newDataStr){
        if (!appStore) {
            appStore = {}
        }

        var newApps = newDataStr.split("|")
        var newPIDs = []
        var newClasses = []
        var classes = []

        // Get Pinned Masks
        var masques = {}
        for (var i=0; i < root.settings.launchers.length; i++){
            var item = root.settings.launchers[i]
            if (item.masque){
                masques[item.name] = item.masque
            }
        }

        //Build New Instances Into AppStore
        for (var i = 0; i < newApps.length; i++){ 
            var app = newApps[i].split(",")
            if (app.length < 5) continue

            var pid = app[0]
            newPIDs.push(pid)
            var name = app[1].trim()
            classes.push(name)
            if (!appStore[name]) newClasses.push(name)
            var workspace = app[4].trim()
            var windowTitle = app[5].trim()

            //Masking & Reassignment
            var stateAssignmentKey = name
            for (var masqueKey in masques){
                var masque = masques[masqueKey]
                var reassign = false
                if (masque.classIncludes && name.includes(masque.classIncludes)){
                    reassign = true
                } else if (masque.cmdIncludes && app[3].includes(masque.cmdIncludes)){
                    reassign = true
                }

                if (reassign){
                    if (appStore[name]){
                        delete appStore[name]
                    }
                    if (!appStore[masqueKey]) {
                        appStore[masqueKey] = {
                            procs: []
                        }
                    }
                    stateAssignmentKey = masqueKey
                    break
                }
            }

            // Create State
            if (name === stateAssignmentKey && !appStore[name]) {
                appStore[name] = {
                    procs: []
                }
            }

            // PID duplicate check
            var hasPID = false
            for (var j=0; j < appStore[stateAssignmentKey].procs.length; j++){
                var instance = appStore[stateAssignmentKey].procs[j]
                if (instance.pid === pid){
                    hasPID = true
                    instance['workspace'] = workspace
                    instance['windowTitle'] = windowTitle
                    break
                }
            }
            if (hasPID) continue

            appStore[stateAssignmentKey].procs.push({
                pid: pid,
                name: name,
                windowTitle: windowTitle,
                workspace: workspace 
            })
        }

        //Remove Dead Instances from AppStore
        for (var name in appStore) {
            var instances = appStore[name].procs
            if (!instances || instances.length === 0) {
                delete appStore[name]
                continue
            }
            for (var i = instances.length - 1; i >= 0; i--) {
                var instance = instances[i]
                if (!newPIDs.includes(instance.pid) || !classes.includes(instance.name)) {
                    instances.splice(i, 1)
                }
            }
            if (instances.length == 0) delete appStore[name]
        }

        //Update Pinned UI State Data
        for (var j=0; j < apps.count; j++){
            var app = apps.get(j)
            if (isAppPinned(app.name)){
                var instances = appStore[app.name] ? appStore[app.name].procs : []
                var hidden = getHiddenCount(app.name)
                var state = "static"
                if (instances.length === 1) state = "static|active"
                else if (instances.length > 1) state = "static|multi-active"
                if (app.type != state || app.instanceCount != instances.length || app.hiddenCount != hidden){
                    apps.set(
                        j, 
                        {
                            name: app.name,
                            nickname: app.nickname,
                            icon: app.icon,
                            command: app.command,
                            options: app.options,
                            type: state,
                            instanceCount: instances.length,
                            hiddenCount: hidden
                        }
                    )
                }
            }
        }

        //Build UI
        for (var name in appStore) {
            var instances = appStore[name].procs
            var first = newApps.find(a => a.includes("," + name + ","))
            if (!first) continue
            var parts = first.split(",")
            var parsed = parseCommand(parts[3], parts[1])

            // Update Pinned Options Data / Skip
            if (isAppPinned(name)) {
                
                //Update Pinned Settings
                for (var i = 0; i < root.settings.launchers.length; i++) {
                    var pinned = root.settings.launchers[i]
                    if (pinned.name !== name)
                        continue

                    if (root.settings.launcherflags.lockOptions.includes(name)) break
                    if (root.settings.launcherflags.ignoreOptions.includes(name)) {
                        var hadOptions = pinned.options.length > 0
                        if (hadOptions) {
                            pinned.options = []
                            root.saveSettings()
                        }
                        break
                    }
                    var filters = root.settings.launcherflags.filters[name]
                    if (filters && filters.length > 0){
                        var includesFilter = false
                        for (var i=0; i < filters.length; i++){
                            if (parsed.options.includes(filters[i])){
                                includesFilter = true
                                break
                            }
                        }
                        if (includesFilter) break
                    }

                    var existingOptions = pinned.options || []
                    var newOptions = parsed.options
                    if (!newOptions || newOptions.length === 0) break 
                    var newStr = JSON.stringify(newOptions[0])
                    var exists = false
                    for (var j = 0; j < existingOptions.length; j++) {
                        if (JSON.stringify(existingOptions[j]) === newStr) {
                            exists = true
                            break
                        }
                    }

                    if (root.settings.launcherflags.setOptions[name] != undefined && newOptions.length + existingOptions.length >= root.settings.launcherflags.setOptions[name]){
                        break
                    }

                    if (!exists) {
                        existingOptions.unshift(newOptions[0])
                        if (existingOptions.length > root.settings.launcherflags.maxOptions){
                            existingOptions = existingOptions.slice(0, -1)
                        }
                        pinned.options = existingOptions
                        root.saveSettings()
                    }

                    break
                }

                continue
            }

            //Add New Apps
            if (newClasses.includes(name)){
                apps.append({
                    name: name,
                    nickname: "",
                    icon: parts[2],
                    command: parsed.command,
                    options: root.settings.launcherflags.ignoreOptions.includes(name) ? "[]" : encodeOptions(parsed.options),
                    type: "active",
                    instanceCount: instances.length,
                    hiddenCount: getHiddenCount(name)
                })

            //Update Non-Pinned UI State Data
            } else {
                for (var j=0; j < apps.count; j++){
                    var app = apps.get(j)
                    if (app.name === name){
                        apps.set(j, {
                            name: app.name,
                            nickname: app.nickname,
                            icon: app.icon,
                            command: app.command,
                            options: root.settings.launcherflags.ignoreOptions.includes(name) ? "[]" : encodeOptions(parsed.options),
                            type: app.type,
                            instanceCount: instances.length,
                            hiddenCount: getHiddenCount(app.name)
                        })
                        break
                    }
                }
            }
        }
    }

    function getStaticApps(){
        var staticApps = root.settings.launchers

        var startProc = false
        for (var i = 0; i < staticApps.length; i++){
            var item = staticApps[i]
            if (item.icon === "*" && !queuedAppClassesForIcons.includes(item.name)){
                queuedAppClassesForIcons.push(item.name)
                startProc = true
            } 
        }
        if (startProc) getAppIconsProc.getIcons(true)
        
        for (var i = 0; i < staticApps.length; i++){ 
            var item = staticApps[i]
            if (!item.icon || !item.command || !item.name) continue

            var matches = getPIDs(item.name)
            var state = "static"
            if (matches.length === 1) state = "static|active"
            else if (matches.length > 1) state = "static|multi-active"

            apps.insert(
                i,
                {
                    name: item.name,
                    nickname: item.nickname ? item.nickname : "",
                    icon: item.icon,
                    command: item.command,
                    options: root.settings.launcherflags.ignoreOptions.includes(item.name) ? "[]" : encodeOptions(item.options),
                    type: state,
                    instanceCount: matches.length,
                    hiddenCount: getHiddenCount(item.name)
                }
            )
        }
    }

    function updateApps(newDataStr){
        clearInactiveApps()
        getActiveApps(newDataStr)
        if (!addedStaticApps){
            addedStaticApps = true
            getStaticApps()
        }
    }

    function openContextMenu(popupObject){
        var contextHiddenCount = getHiddenCount(contextTarget.name)
        var contextIsPinned = isAppPinned(contextTarget.name)
        var items = [
            {"name": contextTarget.nickname ? "Open " + contextTarget.nickname : "Open " + contextTarget.name, "action":"launch", "icon":"open_app"},
            {"name": "Copy cmd", "action":"copy:command", "icon":"copy_content"},
            {
                "name": contextIsPinned ? "Un-Pin" : "Pin", 
                "action":"pin", 
                "icon": contextIsPinned ? "unpin" : "pin"
            },
            {"name":"Open w/ new args", "action":"launch:with", "icon":"terminal"}
        ]

        if (contextTarget.options.length > 0){
            items.splice(1, 0, {"name":"Open w/ last args", "action":"launch:last", "icon":"history"})
            items.splice(2, 0, {"name": "Copy cmd + last args", "action":"copy:args", "icon":"copy_content"})
            for (var i=0; i < contextTarget.options.length; i++){
                if (i > root.settings.launcherflags.maxOptions) { break }
                var index = contextTarget.options.length - 1 - i
                var optSet = contextTarget.options[index]
                items.splice(
                    0, 0,
                    {"name":optSet[0], "action":"launch:custom", "icon":"terminal", "index":index},
                )
            }
        }

        if (contextTarget.command.includes("/")){
            items.splice(Math.min(2 + root.settings.launcherflags.maxOptions, items.length), 0, {"name":"Open In Files", "action":"open", "icon":"open_folder"},)
        }

        if (contextTarget.type.includes("active")){
            items.splice(
                -1, 0,
                {
                    "name": contextHiddenCount > 0 ? "Show" : "Hide", 
                    "action": contextHiddenCount > 0 ? "workspace:show" : "workspace:hide",
                    "icon" : contextHiddenCount > 0 ? "show" : "hide"
                }
            )
        }

        // Masque options
        var currentMasque = getAppMasque(contextTarget.name)
        if (currentMasque !== "") {
            // This app is masquing under another — offer to remove
            items.push({
                "name": "Remove Masque (" + currentMasque + ")",
                "action": "masque:remove",
                "icon": "masked"
            })
        } else {
            // Not masquing — offer to set one
            items.push({
                "name": "Add as Masque...",
                "action": "masque:open",
                "icon": "masked_add"
            })
        }

        // If this is a pinned app with masques under it — offer to manage them
        if (contextIsPinned) {
            var masquesUnder = getMasquesUnder(contextTarget.name)
            if (masquesUnder.length > 0) {
                items.push({
                    "name": "Manage Masques (" + masquesUnder.length + ")...",
                    "action": "masque:manage",
                    "icon": "masked"
                })
            }
        }

        if (appHasNoOptionsFlag(contextTarget.name)){
            items.push(
                {
                    "name": "Toggle No Arg Options: To OFF",
                    "action": "toggleOptions",
                    "icon": "settings"
                }
            )
        } else {
            items.push(
                {
                    "name": "Toggle No Arg Options: To ON",
                    "action": "toggleOptions",
                    "icon": "settings"
                }
            )
        }

        var pids = getPIDs(contextTarget.name)
        if (pids.length > 0){
            var state = appStore[contextTarget.name]
            for (var i=0; i < state.procs.length; i++){
                var instance = state.procs[i]
                items.push({"name":"Close '" + instance.windowTitle.trim() +"'", "action":"close", "icon":"close", "index": i})
            }
            items.push({"name":"Kill all", "action":"kill", "icon":"stop"})
        }

        popup.actions.clear()
        for (var i=0; i < items.length; i++){
            popup.actions.append(items[i])
        }
        popup.height = (items.length * 35) 
        popup.forceOpen(popupObject)
    }

    //CONTEXT MENU ACTIONS
    function launch(data, includeOptions=true, optionsIndex=0){
        var opts = decodeOptions(data.options) 
        var args = [data.command]
        if (includeOptions && opts.length > 0) {
            args = root.combine(args, opts[optionsIndex])
        }
        root.execute(args)
    }

    function togglePin(target) {
        for (var i = 0; i < root.settings.launchers.length; i++) {
            if (root.settings.launchers[i].name === target.name) {
                root.settings.launchers.splice(i, 1)
                delete appStore[target.name]
                return
            }
        }
        
        contextIcon = target.icon
        root.settings.launchers.push({
            name: target.name,
            nickname: target.nickname || "",
            icon: target.icon,
            command: target.command,
            options: decodeOptions(target.options)
        })
    }

    function killApp(pid){
        root.execute(["kill", pid])
    }

    function closeApp(index, name){
        var state = appStore[name]
        var instance = state.procs[index]
        var title = instance.windowTitle
        root.execute( root.newUtill( root.combine( ["--closehyprwindow"], title.split(" ") ) ) )
        state.procs.splice(index, 1)
        if (state.procs.length === 0){
            delete appStore[name]
        }
    }

    function hideInWorkspace(pid){
        var pidAppState = getAppStateFromPID(pid)
        if (pidAppState){
            pidAppState['lastWorkspace'] = pidAppState['workspace'].trim()
        }
        root.execute(['hyprctl', 'dispatch', 'movetoworkspacesilent', 'special:hidden,pid:' + pid])
    }

    function showInDefault(pid){
        var pidAppState = getAppStateFromPID(pid)
        if (pidAppState.lastWorkspace){
            root.execute(['hyprctl', 'dispatch', 'movetoworkspacesilent', pidAppState['lastWorkspace'].trim()+',pid:'+pid])
        } else {
            root.execute(['hyprctl', 'dispatch', 'movetoworkspacesilent', 1+',pid:'+pid])
        }
    }

    function hideAll(data){
        var pids = getPIDs(data.name)
        for (var i=0; i < pids.length; i++){
            hideInWorkspace( pids[i] )
        }
    }

    function showAll(data){
        var pids = getPIDs(data.name)
        for (var i=0; i < pids.length; i++){
            showInDefault( pids[i] )
        }
    }

    function appHasNoOptionsFlag(name){
        return root.settings.launcherflags.ignoreOptions.includes(name)
    }

    function toggleNoOptions(name){
        if (root.settings.launcherflags.lockOptions.includes(name)) return
        if (appHasNoOptionsFlag(name)) {
            for (var j = 0; j < root.settings.launcherflags.ignoreOptions.length; j++){
                if (name === root.settings.launcherflags.ignoreOptions[j]){
                    root.settings.launcherflags.ignoreOptions.splice(j, 1)
                    break
                }
            }
        } else {
            root.settings.launcherflags.ignoreOptions.push(name)
        }
        root.saveSettings()
    }

    function setMasque(targetClass, masqueUnderName) {
        // Sets classIncludes masque on the chosen pinned launcher
        // so targetClass always appears under masqueUnderName
        for (var i = 0; i < root.settings.launchers.length; i++) {
            var launcher = root.settings.launchers[i]
            if (launcher.name === masqueUnderName) {
                launcher.masque = { classIncludes: targetClass }
                root.saveSettings()
                return
            }
        }
    }

    function removeMasque(className) {
        // Removes any masque that references this class
        for (var i = 0; i < root.settings.launchers.length; i++) {
            var launcher = root.settings.launchers[i]
            if (launcher.masque && launcher.masque.classIncludes === className) {
                delete launcher.masque
                root.saveSettings()
                return
            }
        }
    }

    function getAppMasque(className) {
        // Returns the name of the pinned app this class is masquing under, or ""
        for (var i = 0; i < root.settings.launchers.length; i++) {
            var launcher = root.settings.launchers[i]
            if (launcher.masque && launcher.masque.classIncludes === className) {
                return launcher.name
            }
        }
        return ""
    }

    function getPinnedApps() {
        // Returns array of {name, nickname, icon} for all pinned launchers
        var pinned = []
        for (var i = 0; i < root.settings.launchers.length; i++) {
            var l = root.settings.launchers[i]
            pinned.push({
                name:     l.name,
                nickname: l.nickname || l.name,
                icon:     l.icon || ""
            })
        }
        return pinned
    }

    function getMasquesUnder(pinnedName) {
        // Returns array of {classIncludes} for all masques assigned to this pinned app
        var result = []
        for (var i = 0; i < root.settings.launchers.length; i++) {
            var launcher = root.settings.launchers[i]
            if (launcher.name === pinnedName && launcher.masque) {
                // Support both single masque object and future array
                var m = launcher.masque
                if (m.classIncludes) result.push({ className: m.classIncludes })
                if (m.cmdIncludes)   result.push({ className: m.cmdIncludes })
            }
        }
        return result
    }

    // OBJECTS
    Timer{
        id: getAppsTimer
        interval: 10
        running: true
        repeat: true
        onTriggered: {
            if (!addedStaticApps){ 
                getAppsTimer.interval = 650
            }
            if (!getActiveAppsProc.running){
                getActiveAppsProc.running = true
            }
        }
    }

    Process{
        id: getAppIconsProc
        command: root.newUtill(["--getappicons"])

        function getIcons(clearCache){
            if (queuedAppClassesForIcons.length > 0 && !getAppIconsProc.running) {
                var args = clearCache
                    ? root.combine(["--getappicons", "--clearcache"], queuedAppClassesForIcons)
                    : root.combine(["--getappicons"], queuedAppClassesForIcons)
                getAppIconsProc.command = root.newUtill(args)
                getAppIconsProc.running = true
            }
        }

        stdout: StdioCollector{
            onStreamFinished: {
                var text = this.text.trim()
                if (!text) return

                // Result format: "className:/path/to/icon,className2:/path/to/icon2"
                // Split on comma but paths can't contain commas so this is safe
                var entries = text.split(",")
                var iconMap = {}
                for (var e = 0; e < entries.length; e++) {
                    var colonIdx = entries[e].indexOf(":")
                    if (colonIdx === -1) continue
                    var cls  = entries[e].substring(0, colonIdx).trim()
                    var path = entries[e].substring(colonIdx + 1).trim()
                    iconMap[cls] = path
                }

                // Update settings.launchers
                for (var i = 0; i < root.settings.launchers.length; i++) {
                    var launcher = root.settings.launchers[i]
                    if (iconMap[launcher.name]) {
                        launcher.icon = iconMap[launcher.name]
                    }
                }

                // Update apps ListModel so UI reflects immediately without restart
                for (var j = 0; j < apps.count; j++) {
                    var app = apps.get(j)
                    if (iconMap[app.name]) {
                        apps.set(j, {
                            name:          app.name,
                            nickname:      app.nickname,
                            icon:          iconMap[app.name],
                            command:       app.command,
                            options:       app.options,
                            type:          app.type,
                            instanceCount: app.instanceCount,
                            hiddenCount:   app.hiddenCount
                        })
                    }
                }

                // Clear the queue for processed classes
                queuedAppClassesForIcons = queuedAppClassesForIcons.filter(
                    function(cls) { return !iconMap[cls] }
                )

                root.saveSettings()
            }
        }
    }

    Process{
        id: getActiveAppsProc
        command: root.newUtill(["--getactiveapplications"])
        stdout: StdioCollector{
            onStreamFinished: updateApps(this.text)
        }
    } 

    // -- LAUNCHER POPUP
    PopupPanel{
        id: launchPopup
        implicitHeight: 45
        implicitWidth: 575
        sidePadding: 0
        fadingEffectMax: 1.0
        requireFocusGrab: true

        function setAndOpen(launching){
            launchName.text = launching
            launchIcon.source = contextIcon
            launchPopup.forceOpen(appBarWidget)
            launchPopupTimer.running = true
        }

        Timer{
            id: launchPopupTimer
            interval: 1500
            onTriggered:{
                launchPopup.forceClose()
            }
        }

        content: RowLayout {
            anchors.fill: parent
            anchors.topMargin: 8
            spacing: 2

            Image {
                id: launchIcon
                source: ""
                Layout.preferredWidth: 30
                Layout.preferredHeight: 30
                fillMode: Image.PreserveAspectFit
            }

            Text {
                Layout.fillWidth: true
                id: launchName
                color: root.settings.theme.text
                font.family: root.settings.fontFamily
                font.weight: 500
                font.pixelSize: 20
            }
        }
    }

    // -- CUSTOM ARG POPUP
    PopupPanel{
        id: argPopup
        implicitHeight: 45
        implicitWidth: appBarWidget.width
        sidePadding: 0
        fadingEffectMax: 1.0
        requireFocusGrab: true

        function acceptAndCall(execute){
            if (execute) root.execute( root.combine(contextTarget.command.split(" "), inputField.text.split(" ")) )
            argPopup.forceClose()
            inputField.text = ""
            launchPopup.setAndOpen(
                contextTarget.nickname 
                    ? "Launching " + contextTarget.nickname 
                    : "Launching " + contextTarget.name
            )
        } 

        content: RowLayout {
            anchors.fill: parent
            anchors.topMargin: 5
            spacing: 10

            Image {
                id: image
                source: contextIcon
                Layout.preferredWidth: 30
                Layout.preferredHeight: 30
                fillMode: Image.PreserveAspectFit
            }

            TextField {
                Layout.fillWidth: true
                id: inputField
                color: root.settings.theme.text
                cursorDelegate: Rectangle{
                    width: 2
                    color : root.settings.theme.primary
                }
                font.family: root.settings.fontFamily
                font.weight: 500
                font.pixelSize: 15
                background: Rectangle {
                    color: root.settings.theme.surface
                    border.width: 0
                    radius: 15
                }
                onAccepted: argPopup.acceptAndCall(true)
            }

            IconButton{
                iconName: "close"
                iconSize: 35
                tooltipText: "Close Args Popup"
                color: root.settings.theme.text
                onClicked: argPopup.acceptAndCall(false)
            }
        }
    }

    // -- CONTEXT MENU
    PopupPanel{
        id: popup
        implicitHeight: 350
        implicitWidth: popupColumn.implicitWidth + 16
        sidePadding: 0
        fadingEffectMax: 1.0
        property ListModel actions: ListModel{}

        content: ColumnLayout {
            id: popupColumn
            anchors.fill: parent
            spacing: 0
            Repeater {
                model: popup.actions
                delegate: RoundButton {
                    id: actionbtn
                    required property var modelData
                    Layout.fillWidth: true
                    Layout.alignment: Qt.AlignLeft

                    text: modelData.name
                    font.family: root.settings.fontFamily
                    font.weight: 700
                    font.pixelSize: 14

                    icon.source: modelData.icon ? root.iconSource(modelData.icon) : undefined
                    icon.color: "transparent"

                    padding: 5
                    horizontalPadding: 10

                    contentItem: RowLayout {
                        spacing: 6
                        Image {
                            source: actionbtn.icon.source
                            width: 16
                            height: 16
                            fillMode: Image.PreserveAspectFit
                            visible: source != ""
                        }
                        Text {
                            text: actionbtn.text
                            color: root.settings.theme.text
                            font: actionbtn.font
                            horizontalAlignment: Text.AlignLeft
                            elide: Text.ElideNone
                            wrapMode: Text.NoWrap
                            Layout.fillWidth: true
                        }
                    }

                    HoverHandler {
                        id:hoverHandler
                        cursorShape: Qt.PointingHandCursor
                    }

                    background: Rectangle {
                        radius: 6
                        color: hoverHandler.hovered ? root.settings.theme.primary : "transparent"
                    }

                    onClicked: {
                        if (modelData.action === "pin") {
                            togglePin(contextTarget)
                            root.saveSettings()
                            apps.clear()
                            addedStaticApps = false
                        } else if (modelData.action === "debug:data") {
                            var pids = getPIDs(contextTarget.name)
                        } else if (modelData.action === "launch:with") {
                            argPopup.forceOpen(appBarWidget)
                        } else if (modelData.action === "launch:custom") {
                            if (contextTarget.command) { 
                                launch(contextTarget, true, modelData.index) 
                                launchPopup.setAndOpen(
                                    contextTarget.nickname 
                                        ? "Launching " + contextTarget.nickname 
                                        : "Launching " + contextTarget.name
                                )
                            }
                        } else if (modelData.action === "launch:last") {
                            if (contextTarget.command) { 
                                launch(contextTarget, true, 0)
                                launchPopup.setAndOpen(
                                    contextTarget.nickname 
                                        ? "Launching " + contextTarget.nickname 
                                        : "Launching " + contextTarget.name
                                )
                            }
                        } else if (modelData.action === "launch") {
                            if (contextTarget.command) { 
                                launch(contextTarget, false)
                                launchPopup.setAndOpen(
                                    contextTarget.nickname 
                                        ? "Launching " + contextTarget.nickname 
                                        : "Launching " + contextTarget.name
                                )
                            }
                        } else if (modelData.action === "copy:command") {
                            root.copy( contextTarget.command )
                        } else if (modelData.action === "workspace:hide") {
                            hideAll(contextTarget)
                        } else if (modelData.action === "workspace:show") {
                            showAll(contextTarget)
                        } else if (modelData.action === "copy:args"){
                            var opts = decodeOptions(contextTarget.options)
                            var str = contextTarget.command
                            for (var i=0; i < opts[0].length; i++){
                                str += " " + opts[0][i]
                            }
                            root.copy( str )
                        } else if (modelData.action === "open"){
                            var args = contextTarget.command.split(" ")
                            for (var i=0; i < args.length; i++){
                                var arg = args[i]
                                if (arg.startsWith("/")){
                                    root.execute( ["nautilus", arg] )
                                    break 
                                }
                            }
                        } else if (modelData.action === "close") {
                            closeApp(modelData.index, contextTarget.name)
                        } else if (modelData.action === "kill"){
                            var pids = getPIDs(contextTarget.name)
                            for (var i = 0; i < pids.length; i++) {
                                killApp(pids[i])
                            }
                        } else if (modelData.action === "toggleOptions"){
                            toggleNoOptions(contextTarget.name)
                        } else if (modelData.action === "masque:open") {
                            popup.forceClose()
                            masquePopup.forceOpen(appBarWidget)
                            return
                        } else if (modelData.action === "masque:manage") {
                            popup.forceClose()
                            masqueManagePopup.forceOpen(appBarWidget)
                            return
                        } else if (modelData.action === "masque:remove") {
                            removeMasque(contextTarget.name)
                        }

                        popup.forceClose()
                    }
                }
            }
        }
    }

    // -- ADD APP WINDOW
    AddAppWindow {
        id: addAppWindow
        onSaved: function() {
            apps.clear()
            addedStaticApps = false
        }
    }

    // -- ADD BUTTON DROPDOWN
    PopupPanel {
        id: addDropdown
        implicitHeight: 80
        implicitWidth: Math.max(addDropdownColumn.implicitWidth + 32, 220)
        sidePadding: 0
        fadingEffectMax: 1.0
        scrollingEffect: false

        content: ColumnLayout {
            id: addDropdownColumn
            anchors.fill: parent
            spacing: 0

            RoundButton {
                Layout.fillWidth: true
                text: "From Installed Apps"
                font.family: root.settings.fontFamily
                font.pixelSize: 13
                font.weight: 600
                padding: 6
                horizontalPadding: 16
                contentItem: RowLayout {
                    spacing: 6
                    Image {
                        source: root.iconSource("search")
                        width: 14; height: 14
                        fillMode: Image.PreserveAspectFit
                    }
                    Text {
                        text: parent.parent.text
                        font: parent.parent.font
                        color: root.settings.theme.text
                    }
                }
                background: Rectangle {
                    radius: 6
                    color: addHov1.hovered ? root.settings.theme.primary : "transparent"
                    opacity: addHov1.hovered ? 0.18 : 1
                }
                HoverHandler { id: addHov1; cursorShape: Qt.PointingHandCursor }
                onClicked: {
                    addDropdown.forceClose()
                    addAppWindow.openExisting()
                }
            }

            RoundButton {
                Layout.fillWidth: true
                text: "Custom App"
                font.family: root.settings.fontFamily
                font.pixelSize: 13
                font.weight: 600
                padding: 6
                horizontalPadding: 16
                contentItem: RowLayout {
                    spacing: 6
                    Image {
                        source: root.iconSource("settings")
                        width: 14; height: 14
                        fillMode: Image.PreserveAspectFit
                    }
                    Text {
                        text: parent.parent.text
                        font: parent.parent.font
                        color: root.settings.theme.text
                    }
                }
                background: Rectangle {
                    radius: 6
                    color: addHov2.hovered ? root.settings.theme.primary : "transparent"
                    opacity: addHov2.hovered ? 0.18 : 1
                }
                HoverHandler { id: addHov2; cursorShape: Qt.PointingHandCursor }
                onClicked: {
                    addDropdown.forceClose()
                    addAppWindow.openCustom()
                }
            }
        }
    }

    // -- MASQUE SELECTOR POPUP
    PopupPanel {
        id: masquePopup
        implicitHeight: masqueColumn.implicitHeight + 16
        implicitWidth: Math.max(masqueColumn.implicitWidth + 32, 240)
        sidePadding: 0
        fadingEffectMax: 1.0
        scrollingEffect: false

        content: ColumnLayout {
            id: masqueColumn
            anchors.fill: parent
            spacing: 0

            Text {
                text: "Masque under:"
                color: root.settings.theme.text
                opacity: 0.55
                font.family: root.settings.fontFamily
                font.pixelSize: 12
                font.weight: 600
                Layout.leftMargin: 10
                Layout.topMargin: 6
                Layout.bottomMargin: 2
            }

            Repeater {
                model: appBarWidget.getPinnedApps()
                delegate: RoundButton {
                    required property var modelData
                    visible: modelData.name !== appBarWidget.contextTarget.name
                    Layout.fillWidth: true
                    padding: 6
                    horizontalPadding: 16

                    contentItem: RowLayout {
                        spacing: 8
                        Image {
                            source: modelData.icon && modelData.icon !== "*"
                                ? modelData.icon
                                : root.iconSource("open_app")
                            width: 20
                            height: 20
                            sourceSize.width: 20
                            sourceSize.height: 20
                            fillMode: Image.PreserveAspectFit
                            smooth: true
                        }
                        Text {
                            text: modelData.nickname !== modelData.name
                                ? modelData.nickname + "  (" + modelData.name + ")"
                                : modelData.name
                            font.family: root.settings.fontFamily
                            font.pixelSize: 13
                            font.weight: 500
                            color: root.settings.theme.text
                            Layout.fillWidth: true
                        }
                    }

                    background: Rectangle {
                        radius: 6
                        color: masqueHov.hovered ? root.settings.theme.primary : "transparent"
                        opacity: masqueHov.hovered ? 0.18 : 1
                    }
                    HoverHandler { id: masqueHov; cursorShape: Qt.PointingHandCursor }
                    onClicked: {
                        setMasque(appBarWidget.contextTarget.name, modelData.name)
                        masquePopup.forceClose()
                    }
                }
            }
        }
    }

    // -- MASQUE MANAGE POPUP (remove masques from a pinned app)
    PopupPanel {
        id: masqueManagePopup
        implicitHeight: masqueManageColumn.implicitHeight + 16
        implicitWidth: Math.max(masqueManageColumn.implicitWidth + 32, 240)
        sidePadding: 0
        fadingEffectMax: 1.0
        scrollingEffect: false

        content: ColumnLayout {
            id: masqueManageColumn
            anchors.fill: parent
            spacing: 0

            Text {
                text: "Remove masque:"
                color: root.settings.theme.text
                opacity: 0.55
                font.family: root.settings.fontFamily
                font.pixelSize: 12
                font.weight: 600
                Layout.leftMargin: 10
                Layout.topMargin: 6
                Layout.bottomMargin: 2
            }

            Repeater {
                model: appBarWidget.contextTarget
                    ? appBarWidget.getMasquesUnder(appBarWidget.contextTarget.name)
                    : []
                delegate: RoundButton {
                    required property var modelData
                    Layout.fillWidth: true
                    padding: 6
                    horizontalPadding: 16

                    contentItem: RowLayout {
                        spacing: 8
                        Image {
                            source: root.iconSource("masked")
                            width: 18; height: 18
                            sourceSize.width: 18; sourceSize.height: 18
                            fillMode: Image.PreserveAspectFit
                            opacity: 0.7
                        }
                        Text {
                            text: modelData.className
                            font.family: root.settings.fontFamily
                            font.pixelSize: 13
                            font.weight: 500
                            color: root.settings.theme.text
                            Layout.fillWidth: true
                        }
                        Image {
                            source: root.iconSource("close")
                            width: 14; height: 14
                            sourceSize.width: 14; sourceSize.height: 14
                            fillMode: Image.PreserveAspectFit
                            opacity: 0.6
                        }
                    }

                    background: Rectangle {
                        radius: 6
                        color: manageHov.hovered ? "#e05555" : "transparent"
                        opacity: manageHov.hovered ? 0.18 : 1
                    }
                    HoverHandler { id: manageHov; cursorShape: Qt.PointingHandCursor }
                    onClicked: {
                        removeMasque(modelData.className)
                        masqueManagePopup.forceClose()
                    }
                }
            }
        }
    }

    // -- WIDGET ROW
    RowLayout{
        id: row
        anchors.centerIn: parent
        spacing: 15

        Tooltip {
            id: tooltip
            text: ""

            function openAndSet(text, point){
                tooltip.text = text
                tooltip.toggleOnTo(point)
            }
        }

        Repeater{
            model: appBarWidget.apps
            
            // APP BUTTON
            delegate: RoundButton {
                id: button

                property string name: model.name 
                property string nickname: model.nickname
                property string iconSource: model.icon 
                property string command: model.command
                property string options: model.options
                property string type: model.type
                property int instanceCount: model.instanceCount
                property int hiddenCount: model.hiddenCount
                property string tooltipText: nickname ? nickname : name
                property bool pinned: isAppPinned(name)

                padding: 1
                Layout.preferredWidth: 25
                Layout.preferredHeight: 25
                font.family: root.settings.fontFamily

                background: Rectangle {
                    radius: button.radius
                    color: "transparent"
                    border.color: 'transparent'
                    border.width: 0
                }

                MouseArea {
                    anchors.fill: parent
                    acceptedButtons: Qt.LeftButton | Qt.RightButton
                    onClicked: (mouse) => {
                        if (mouse.button == Qt.LeftButton){
                            if (!type.includes("active")){
                                appBarWidget.contextIcon = content.getImageIcon()
                                launch(model) 
                                launchPopup.setAndOpen(
                                    nickname 
                                        ? "Launching " + nickname 
                                        : "Launching " + name
                                )
                            } 
                        } else if (mouse.button == Qt.RightButton){
                            if (!appBarWidget.contextTarget || appBarWidget.contextTarget.command != command){
                                appBarWidget.contextIcon = content.getImageIcon()
                                appBarWidget.contextTarget = {
                                    name: name,
                                    nickname: nickname,
                                    icon: iconSource,
                                    command: command.trim(),
                                    options: decodeOptions(options),
                                    type: type
                                }
                                openContextMenu(button)
                            } else {
                                if (!popup.visible && appBarWidget.contextTarget.command == command){
                                    appBarWidget.contextIcon = content.getImageIcon()
                                    appBarWidget.contextTarget = {
                                        name: name,
                                        nickname: nickname,
                                        icon: iconSource,
                                        command: command.trim(),
                                        options: decodeOptions(options),
                                        type: type
                                    } 
                                    openContextMenu(button)
                                } else {
                                    popup.forceClose()
                                }
                            }
                        } 
                    }

                    HoverHandler {
                        id:hoverHandler
                        cursorShape: Qt.PointingHandCursor
                        onHoveredChanged : {
                            tooltip.openAndSet(
                                button.nickname ? button.nickname : button.name,
                                hoverHandler.point
                            )
                        }
                    }
                }

                contentItem: Item {
                    id: content
                    anchors.centerIn: parent

                    function getImageIcon(){
                        return image.source
                    }

                    Image {
                        id: image
                        source: iconSource
                        anchors.centerIn: parent
                        width: parent.width * 1.5
                        height: parent.height * 1.5
                        fillMode: Image.PreserveAspectFit
                        opacity: hiddenCount > 0 ? 0.4 : 1.0
                    }
                    Rectangle {
                        width: parent.width - 2
                        height: 4
                        radius: 2
                        anchors.bottom: parent.bottom
                        anchors.horizontalCenter: parent.horizontalCenter
                        anchors.bottomMargin: -12
                        color: {
                            if (instanceCount === 0) return "transparent"
                            if (hiddenCount === instanceCount) return "#666666"
                            if (hiddenCount > 0) return "#ffaa00"
                            return root.settings.theme.primary
                        }
                    }
                }
            }
        }

        // ── Add App button — always at the end ───────────────────
        RoundButton {
            id: addAppButton
            padding: 1
            Layout.preferredWidth: 32
            Layout.preferredHeight: 32
            font.family: root.settings.fontFamily

            background: Rectangle {
                radius: addAppButton.radius
                color: addHovMain.hovered
                    ? root.settings.theme.primary
                    : "transparent"
                opacity: addHovMain.hovered ? 0.25 : 0.4
                border.color: root.settings.theme.text
                border.width: 1
            }

            contentItem: Text {
                text: "+"
                color: root.settings.theme.text
                font.family: root.settings.fontFamily
                font.pixelSize: 20
                font.weight: 300
                horizontalAlignment: Text.AlignHCenter
                verticalAlignment: Text.AlignVCenter
            }

            HoverHandler { id: addHovMain; cursorShape: Qt.PointingHandCursor }

            onClicked: addDropdown.toggle(addAppButton)
        }
    }
}